(ns nameserver.custom-tld-test
  (:require [clojure.test :refer [deftest is]]
            [nameserver.resolver :as resolver]
            [nameserver.custom-tld :as ctld]))

(def ipns-name "k51qzi5uqu5dg6lcd99r9gmb963kgugjinxxggwy7o93oagk3f2eg3qcjh7127")

(deftest dnslink-txt-for-key-derived-label
  (let [r (ctld/custom-tld-resolver #{"hogehoge."})
        result (resolver/-resolve r (str ipns-name ".hogehoge.") "TXT" "IN")]
    (is (= :ok (:status result)))
    (is (= (str "dnslink=/ipns/" ipns-name)
           (-> result :answers first :zone/rdata :zone/text)))
    (is (= (str ipns-name ".hogehoge.") (-> result :answers first :zone/name)))))

(deftest dnslink-txt-via-underscore-dnslink-prefix
  (let [r (ctld/custom-tld-resolver #{"hogehoge."})
        result (resolver/-resolve r (str "_dnslink." ipns-name ".hogehoge.") "TXT" "IN")]
    (is (= :ok (:status result)))
    (is (= (str "dnslink=/ipns/" ipns-name) (-> result :answers first :zone/rdata :zone/text)))))

(deftest cname-to-gateway-when-configured
  (let [r (ctld/custom-tld-resolver #{"hogehoge."} "ipns.dweb.link")
        result (resolver/-resolve r (str ipns-name ".hogehoge.") "A" "IN")]
    (is (= :ok (:status result)))
    (is (= "CNAME" (-> result :answers first :zone/type)))
    (is (= (str ipns-name ".ipns.dweb.link.") (-> result :answers first :zone/rdata :zone/target)))))

(deftest nodata-for-a-when-no-gateway-configured
  (let [r (ctld/custom-tld-resolver #{"hogehoge."})
        result (resolver/-resolve r (str ipns-name ".hogehoge.") "A" "IN")]
    (is (= :nodata (:status result)))))

(deftest nxdomain-for-non-ipns-label
  (let [r (ctld/custom-tld-resolver #{"hogehoge."})
        result (resolver/-resolve r "not-a-real-ipns-label.hogehoge." "TXT" "IN")]
    (is (= :nxdomain (:status result)))))

(deftest refused-for-unconfigured-suffix
  (let [r (ctld/custom-tld-resolver #{"hogehoge."})
        result (resolver/-resolve r (str ipns-name ".com.") "TXT" "IN")]
    (is (= :refused (:status result)))))
